# Set locale to US with utf-8 support
export LANG='en_US.UTF-8'

# Add ~/bin to PATH with highest priority
export PATH=$HOME/bin:$PATH

# Load colors for use in zsh (prompt mainly)
autoload -Uz colors && colors

# Expand $ etc in prompt
setopt PROMPT_SUBST

# Typing directory name cd into it
setopt AUTOCD

# Load git prompt functions (used in precmd)
if [[ -f ~/.git-prompt.sh ]] then
	source ~/.git-prompt.sh
fi

# Git prompt config
GIT_PS1_SHOWDIRTYSTATE=true
GIT_PS1_SHOWUPSTREAM="auto"
GIT_PS1_SHOWUNTRACKEDFILES=true
GIT_PS1_SHOWCOLORHINTS=true

# Prompt parts
LEFT_PROMPT='%n@%m:%~'
COLORED_LEFT_PROMPT='%{$fg_bold[green]%}%n@%m%{$reset_color%}:%{$fg_no_bold[blue]%}%~ %{$reset_color%}'
COLORED_RIGHT_PROMPT='%{$fg_bold[black]%}[%j] | %*%(?..%{$fg_no_bold[red]%} %?¬)%{$reset_color%}'
NEWLINE=$'\n'

# Helper function for precmd (prompt) configuration
# Adds necessary padding to right align jobs | date (last exit status)
right_align() {
	local PADDING=''
	local PADDING_CHAR=' '

	LEFT_PROMPT_SIZE="${#${(%)LEFT_PROMPT}}"
	RIGHT_PROMPT_SIZE="${#${(%)RIGHT_PROMPT}}"

	# Add +2 for Konsole support (scroll bar takes an exta column)
	(( PADDING_SIZE=${COLUMNS} - (${RIGHT_PROMPT_SIZE} + ${LEFT_PROMPT_SIZE} + 2) ))

	for (( i = 0; i < ${PADDING_SIZE}; i++ ));
	do PADDING=${PADDING}${PADDING_CHAR};
	done
	echo $PADDING
}

# Prompt configuration
precmd() {
	local LAST_EXIT_STATUS=$?
	if [[ $LAST_EXIT_STATUS -eq 0 ]]; then
		RIGHT_PROMPT='[%j] | %*'
	elif [[ $LAST_EXIT_STATUS -ge 100 ]]; then
		RIGHT_PROMPT='[%j] | %* %?¬ '
	else
		RIGHT_PROMPT='[%j] | %* %?¬'
	fi
	__git_ps1 "${COLORED_LEFT_PROMPT}$(right_align)${COLORED_RIGHT_PROMPT}${NEWLINE}" " %# " "%s"
}

# History settings
setopt HIST_IGNORE_ALL_DUPS
setopt SHARE_HISTORY

if [[ -x /usr/bin/stty ]]; then
	/usr/bin/stty -ixon		# Disable <C-s> <C-q>
fi
unsetopt FLOW_CONTROL	# Disable <C-s> <C-q>
setopt noflowcontrol	# Disable <C-s> <C-q>
bindkey -e				# Use emacs keys for line editing (This hurts a lot)

# Keep 1000 lines of history within the shell and save it to ~/.zsh_history:
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.zsh_history

# Use modern completion system
autoload -U compinit && compinit
if [[ -f ~/.git-completion.zsh ]] then
	zstyle -T ':completion:*:*:git:*' script ~/.git-completion.zsh
fi

# Completion configuration
# :completion:function:completer:command:argument:tag.
zstyle ':completion:*' completer _complete _correct _approximate
zstyle ':completion:*' group-name '' # Group suggestions by tags
if [[ -x /usr/bin/dircolors ]]; then
	eval "$(dircolors -b)" # Load ls colors
fi
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' menu select # Enable arrows
zstyle ':completion:*' use-compctl false # Disable old completion system
zstyle ':completion:*' verbose true

# Cool kill completion
# However, there is one even more special effect you can use with the general pattern form. By turning on `backreferences' with `(#b)' inside the pattern, parentheses are active and the bits they match can be coloured separately. You do this by extending the list of colours, each code preceded by an `=' sign, and the extra elements will be used to colour what the parenthesis matched. Here's another example for `kill', which turns the process number red, but leaves the rest alone. 
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

export CLICOLOR=1 # Enable command line colors on Mac / BSD (eg: ls output)

# Colored man pages with less
man() {
	env LESS_TERMCAP_mb=$'\E[01;31m' \
		LESS_TERMCAP_md=$'\E[01;38;5;74m' \
		LESS_TERMCAP_me=$'\E[0m' \
		LESS_TERMCAP_se=$'\E[0m' \
		LESS_TERMCAP_so=$'\E[38;5;246m' \
		LESS_TERMCAP_ue=$'\E[0m' \
		LESS_TERMCAP_us=$'\E[04;38;5;146m' \
		man "$@"
}
export GROFF_NO_SGR=1

# Set proper term variable for weechat"
# alias weechat='TERM=screen-256color weechat'

# Aliases
if [[ -x "$(command -v dircolors)" ]]; then
	alias ls='ls --color=auto'
	alias grep='grep --color=auto'
fi

# Git aliases
alias gst='git status'
alias gsb='git status -sb'
alias glo='git log --oneline --decorate'
alias glg='git log --decorate'
alias glga='git log --decorate --all'
alias glgg='git log --decorate --graph --oneline'
alias glgga='git log --decorate --graph --oneline --all'
alias gck='git checkout'
alias grv='git remote -v'
alias grs='git remote show'
alias grso='git remote show origin'
alias gp='git pull'
alias gf='git fetch --all'
alias gres='git reset'
alias gdt='git difftool'
alias gmt='git mergetool'
alias gb='git branch'
alias gcm='git commit'
alias gd='git diff'
alias gds='git diff --staged'

#alias tmux='tmux -2' # Force tmux to use 256 colors

alias l='ls -lFh'
alias la='ls -laFh'

alias ez='vim ~/.zshrc'
alias sz='source ~/.zshrc'

if [[ -x $HOME/.brew/bin/brew ]]; then
	export PATH="$HOME/.brew/bin:$PATH"
	export HOMEBREW_CACHE="/tmp/homebrew_cache"
fi

# Key bindings
bindkey "\e[1~" beginning-of-line
bindkey "\e[4~" end-of-line
bindkey "\e[3~" delete-char

#if [[ "$COLORTERM" == "xfce4-terminal" && -z "$TMUX" ]]; then
#    export TERM=xterm-256color
#fi

alias shrug='cat ~/.shrug'

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# Use ripgrep witf fzf instead of default find
#if [[ -x "$(command -v rg)" ]]; then
#	export FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow --glob "!.git/*"'
#fi
